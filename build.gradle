plugins {
    id 'groovy'
    id 'idea'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'net.saliman.cobertura' version '2.6.0'
    id 'nebula.integtest' version '5.2.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()

    implementation 'org.codehaus.gpars:gpars:1.2.1'
    implementation 'org.zeroturnaround:zt-exec:1.8'
    implementation 'net.masterthought:cucumber-reporting:3.2.0'
    testImplementation 'com.netflix.nebula:nebula-test:6.3.0'

    integTestImplementation 'com.netflix.nebula:nebula-test:6.3.0'
}

cobertura {
    coverageIgnoreTrivial = true
    auxiliaryClasspath += project.sourceSets.main.runtimeClasspath
}

test {
    finalizedBy coberturaReport
}

integrationTest {
    finalizedBy coberturaReport
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

pluginBundle {
    website = "https://github.com/awbdallas/gradle-cucumber-jvm-plugin"
    vcsUrl = "https://github.com/awbdallas/gradle-cucumber-jvm-plugin"
    description = "The gradle cucumber-jvm plugin provides the ability to run cucumber acceptance tests directly from a gradle build."

    plugins {
        cucumberJvm {
            tags = ["cucumber", "java", "testing"]
            id = "com.awbriggs.cucumber-jvm"
            displayName = "Cucumber-JVM Plugin"
            version = '0.1'
        }
    }
}
