plugins {
    id 'groovy'
    id 'idea'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'net.saliman.cobertura' version '2.2.8'
    id 'net.researchgate.release' version '2.1.2'
    id 'nebula.integtest' version '3.4.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


repositories {
    jcenter()
}

dependencies {
    compile gradleApi()

    compile 'org.codehaus.gpars:gpars:1.2.1'
    compile 'org.zeroturnaround:zt-exec:1.8'
    compile 'net.masterthought:cucumber-reporting:3.2.0'

    testCompile 'com.netflix.nebula:nebula-test:6.3.0'
    integTestCompile 'com.netflix.nebula:nebula-test:6.3.0'
}

sourceSets {
    test {
        runtimeClasspath = runtimeClasspath.minus files("$gradle.gradleHomeDir/lib/commons-io-1.4.jar")
    }
}


cobertura {
    coverageIgnoreTrivial = true
    auxiliaryClasspath += project.sourceSets.main.runtimeClasspath
}

test {
    finalizedBy coberturaReport
}

integrationTest {
    finalizedBy coberturaReport
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

pluginBundle {
    website = "https://github.com/awbdallas/gradle-cucumber-jvm-plugin"
    vcsUrl = "https://github.com/awbdallas/gradle-cucumber-jvm-plugin"
    description = "The gradle cucumber-jvm plugin provides the ability to run cucumber acceptance tests directly from a gradle build."

    plugins {
        cucumberJvm {
            tags = ["cucumber", "java", "testing"]
            id = "com.awbriggs.cucumber-jvm"
            displayName = "Cucumber-JVM Plugin"
            version = '0.1'
        }
    }
}
